% !TeX root = gentle.tex
% !TeX Program=XeLaTeX

\section{The square root of two is irrational}

Suppose that $\sqrt{2}$ is rational so that $\sqrt{2}=m/n$. Without loss of generality, assume that $m,n$ are \emph{coprime}, that is, they have no common factor. Then $m^2/n^2=2$ and $m^2=2n^2$, so $2$ must divide $m$. Therefore, $2^2$ must divide $2n^2$ and hence $n$ is also divisible by $2$, contracting the assumption that $m,n$ are coprime. This section contains a Lean proof of the central claim of the proof that if $m,n$ are coprime then $m^2\neq 2n^2$.

The libraries !Mathlib.Data.Nat.Prime! and !Std.Data.Nat.Gcd! must be imported.

A !lemma! is just a different name for a !theorem!. This lemma proves that $a^2=a\cdot a$ using !rw! on the successor function and the base case of the definition for taking the power of a number.
\begin{Verbatim}
lemma pow_two (a : ℕ) : a ^ 2 = a * a := by
  rw [Nat.pow_succ]
    -- Nat.pow_succ:  n ^ succ m = n ^ m * n,
    --   where n = a, m = 1, succ m = 1 + 1
    -- New goal is a ^ 1 * a = a * a
  rw [pow_one]
    -- pow_one: a ^ 1 = a
  done
\end{Verbatim}

The second lemma proves that if $2 | m^2$ then $2 | m$ using the first lemma and the theorem that if there is a prime factor of $a*b$ then it is a prime factor of either $a$ or $b$. When the hypothesis !h! which is now $2| m \cdot m$ is rewritten, since $a=b=m$ the hypothesis becomes the disjunction $2 | m \vee 2 | m$.
\begin{Verbatim}[firstnumber=last]
lemma even_of_even_sqr (m : ℕ)
     (h : 2 ∣ m ^ 2) : 2 ∣ m := by
  rw [pow_two] at h
    -- pow_two (lemma): a ^ 2 = a * a,
    --   where a = m
    -- New goal is 2 ∣ m
  rw [prime_two.dvd_mul] at h
    -- prime_two: 2 is prime
    -- dvd_mul: if p is prime 
    --   then p ∣ m * m ↔ p ∣ m ∨ p ∣ m,
    --   where p = 2, m = m,
    -- Apply to h : 2 | m * m → (2 | m ∨ 2 | m)
    -- h is now 2 | m ∨ 2 | m, goal is still 2 ∣ m
\end{Verbatim}

Split the disjunctive hypothesis $2 | m \vee 2 | m$ into two identical hypotheses; each one is exactly the goal of the lemma.

\begin{Verbatim}[firstnumber=last]
  rcases h with h₁ | h₁
    -- Splits disjunctive hypothesis h: 
    --   2 | m ∨ 2 ∣ m  into
    --   two (identical ) subformulas 2 ∣ m, 2 ∣ m
\end{Verbatim}
Prove both the (identical) subformulas.
\begin{Verbatim}[firstnumber=last]
  · exact h₁
      -- 2 ∣ m proves 2 ∣ m
  · exact h₁
      -- 2 ∣ m proves 2 ∣ m
  done
\end{Verbatim}

\boxed{Tactic \Verb+rcases+}{rcases}{Given a hypothesis or goal that is a disjunction \Verb+A∨B+ the tactic \Verb+rcases+ splits it into two sub-hypotheses or subgoals \Verb+A+ and \Verb+B+.\smallskip\\
For a disjunctive hypothesis, prove the goal under both sub-hypotheses. For a disjunctive goal, prove one of the subgoals.}

To prove the theorem, we assume $m^2=2n^2$ and try to prove a contradiction.
\begin{Verbatim}[firstnumber=last]
theorem sqr_not_even (m n : ℕ) (coprime_mn : Coprime m n) :
    m ^ 2 ≠ 2 * n ^ 2 := by
  intro sqr_eq
    -- Assume sqr_eq: m ^ 2 = 2 * n ^ 2 and
    --   prove a contradiction
\end{Verbatim}

Add the hypothesis $2|m$. By the lemma !apply even_of_even_sqr!, it is sufficient to prove $2 | m^2$ which becomes the new goal. Rewrite using the assumption $m^2=2n^2$ that was introduced to prove a contradiction and apply the trivial theorem that $a | a\cdot b$ proving the hypothesis.

\begin{Verbatim}[firstnumber=last]
  have two_dvd_m : 2 ∣ m := by
    apply even_of_even_sqr
      --  even_of_even_sqr (lemma): 2 ∣ m ^ 2 → 2 ∣ m
      --  New goal is 2 ∣ m ^ 2
    rw [sqr_eq]
      -- sqr_eq: m ^ 2 = 2 * n ^ 2.
      -- Apply to the current goal.
      -- The new goal is 2 ∣ 2 * n ^ 2
    apply dvd_mul_right
      -- dvd_mul_right: a ∣ a * b,
      --   where a = 2, b = n ^ 2
      -- Apply to the current goal to prove two_dvd_m : 2 ∣ m
\end{Verbatim}

The definition of divisibility is: $a|b\leftrightarrow \exists c ( a\cdot c = b)$. In Lean this is expressed by the forward direction (!mp!) of !dvd_iff_exists_eq_mul_left! applied to the hypothesis !2 ∣ m!. The goal is now to find such a $c$.
\begin{Verbatim}[firstnumber=last]
  have h : ∃ c, m = c * 2 := by
    apply dvd_iff_exists_eq_mul_left.mp two_dvd_m
      -- dvd_iff_exists_eq_mul_left: a ∣ b ↔ ∃ c, b = c * a
      --   where a = 2, b = m, c = c
      -- Use MP with two_dvd_m: 2 ∣ m to prove h
\end{Verbatim}
Given an existential formula such as $\exists c P(c)$, let $c$ be some value that satisfies $P$.
\begin{Verbatim}[firstnumber=last]
  rcases h with ⟨k, meq⟩
    -- h : ∃ c, m = c * 2 is an existential formula
    --   rcases on h:
    --     k is the free variable for the bound variable c
    --     meq : m = k * 2 is a new hypothesis
    -- Type ⟨ ⟩ using \< \>
\end{Verbatim}

\boxed{Tactic \Verb+rcases+}{rcases-with}{\Verb+rcases h with ⟨v, h'⟩+ means given a hypothesis \Verb+h+, let \Verb+v+ be a value such that the new goal is \Verb+h'+.}

\begin{center}
\setlength{\fboxrule}{1pt}
\fbox{\parbox{.6\textwidth}{By now you are certainly quite skillful in Lean, so the extent of the comments in the source code will be reduced.}}
\end{center}

We now prove a sequence of five hypotheses.
\begin{Verbatim}[firstnumber=last]
  have h₁ : 2 * (2 * k ^ 2) = 2 * n ^ 2 := by
    rw [← sqr_eq]
      -- sqr_eq : m ^ 2 = 2 * n ^ 2
      -- ← is right to left rewriting of 2 * n ^ 2 in h₁
      -- New goal is 2 * (2 * k ^ 2) = m ^ 2
    rw [meq]
      -- Rewrite m = k * 2 in h₁
      -- New goal is  2 * (2 * k ^ 2) = (k * 2) ^ 2
    ring
      -- Prove goal by using the ring axioms
\end{Verbatim}

\boxed{Tactic \Verb+ring+}{ring}{Proves equalities that can be proved directly from the axioms of a commutative ring without taking any hypotheses into account. For the ring of integers, only addition, subtraction, multiplication and powers by natural numbers can be used. The division operation is not defined in the ring of integers because $1/2$ is not an integer.}

It is easy to see that $2(2k^2)=(k\cdot 2)^2$ can be proved using only the definition of positive powers of integers as repeated multiplication, and the laws of associativity and commutativity of the integers.

The theorem !mul_right_inj'! is applied assuming that $2\neq 0$, but this is a simple property of natural numbers which can be proved by the tactic !norm_num!.
\begin{Verbatim}[firstnumber=last]
  have h₂ : 2 * k ^ 2 = n ^ 2 := by
    apply (mul_right_inj' (by norm_num : 2 ≠ 0)).mp h₁
      -- mul_right_inj': a ≠ 0 → (a * b = a * c ↔ b = c)
      --   where a = 2, b = 2 * k ^ 2, c = n ^ 2
      -- norm_num: solves equalities and inequalities like 2 ≠ 0
      -- Since 2 ≠ 0, MP on h₁ proves h₂
\end{Verbatim}

\boxed{Tactic \Verb+norm\_num+}{norm-num}{Proves numerical equalities and inequalities that do not use variables.}

\begin{Verbatim}[firstnumber=last]
  have h₃ : 2 ∣ n := by
    apply even_of_even_sqr
      -- even_of_even_sqr (lemma) : 2 ∣ m ^ 2 → 2 ∣ m
      --   where m = n
      -- New goal is 2 ∣ n ^ 2
    rw [← h₂]
      -- Rewrite right-to-left of h₂ in the goal
      -- New goal is 2 ∣ 2 * k ^ 2
    apply dvd_mul_right
      -- dvd_mul_right : a ∣ a * b,
      --   where a = 2, b = k ^ 2 to prove h₃
\end{Verbatim}
\begin{Verbatim}[firstnumber=last]
    have h₄ : 2 ∣ Nat.gcd m n := by
      apply Nat.dvd_gcd
        -- Nat.dvd_gcd : (k ∣ m ∧ k | m) → k ∣ gcd m n
        --   k = 2, m = m, n = n
        -- New goals are 2 ∣ m and 2 ∣ n
      · exact two_dvd_m
        -- First goal is two_dvd_m
      . exact h₃
        -- Second goal is h₃
\end{Verbatim}
\begin{Verbatim}[firstnumber=last]
  have h₅ : 2 ∣ 1 := by
    rw [Coprime.gcd_eq_one] at h₄
      -- if m and n are coprime then gcd m n = 1,
      --   where m = 2 and n = 1
      -- Apply to h₄
      -- New goals are 2 ∣ 1 and m, n are coprime
    exact h₄
      -- Proves 2 ∣ 1
    exact coprime_mn
      -- Assumption that m, n are coprime
\end{Verbatim}


The sequence of hypotheses that have been proved terminates in !h₅ : 2 ∣ 1!, but !norm_num! can prove that this is the negation of the true formula   !1 ∣ 2!, thereby deriving a contradiction.

\begin{Verbatim}[firstnumber=last]
  norm_num at h₅
    -- Goal is 2 ∣ 1
    -- norn_num can prove that this is False
    -- Proving the contradiction of the initial assumption
  done
\end{Verbatim}
