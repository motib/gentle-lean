% !TeX root = gentle.tex
% !TeX Program=XeLaTeX

\section{Induction}

\subsection{Sum of a sequence}

The following theorem is the one usually used to introduce induction.
\[
\sum_{k=1}^{n} k = \frac{n(n+1)}{2}\,.
\]
Here is the preamble for the proof of this theorem.
\begin{Verbatim}
import Mathlib.Tactic
import Mathlib.Data.Nat.Basic
import Mathlib.Data.Finset.Basic
import Mathlib.Algebra.BigOperators.Basic
namespace gentle
open Nat Finset BigOperators
\end{Verbatim}

The library !Finset! supports finite sets and !BigOperators! enables the use of the operators for sum and product.
\[
\sum_{x\in s} f(x)\,, \quad \quad \prod_{x\in s} f(x)\,.
\]
\boxed{Tip: \Verb+Finset+, \Verb+BigOperators+}{finset}{Given a finite set of numbers \Verb+s+ and a function \Verb+f+ over these numbers, \Verb+Finset+ defines the expressions \Verb+∑ x in s f x+ and \Verb+∏ x in s f x+, where \Verb+BigOperators+ enables the use of  \Verb+∑+ and \Verb+∏+.}

\boxed{Tip: \Verb+range+}{range}{\Verb+range n+ is defined as the set of natural numbers less than \Verb+n+.}

Since !range n! is $0,1,2,\ldots,n-1$, to get the set of natural numbers $0,1,2,\ldots,n$ the proof will use !range (n + 1)!.\footnote{The result of adding the numbers from $k=0$ to $k=n$ is obviously the same as adding from $k=1$ to $k=n$.}  We will use $r(n+1)$ to denote this sequence of numbers:
\[
\sum_{i=r(n+1)} i = \frac{n(n+1)}{2}\,.
\]
This first step in the proof is to replace the goal with the symmetric goal
\[
\frac{n(n+1)}{2} = \sum_{i=r(n+1)} i\,.
\]
\begin{Verbatim}[firstnumber=last]
theorem sum_id (n : ℕ) :
    ∑ i in range (n + 1), i = n * (n + 1) / 2 := by
  symm
\end{Verbatim}

\boxed{Tactic: \Verb+symm+}{symm}{Replace an equality \Verb+t = u+ by \Verb+u = t+.}

The next step is to prove the trivial hypothesis $0<2$. The hypothesis is \emph{anonymous} and is subsequently accessed as !this!.
\begin{Verbatim}[firstnumber=last]
  have : 0 < 2 := by norm_num
\end{Verbatim}

\boxed{Tip: Anonymous hypothesis}{this}{A hypothesis can be unamed and can subsequently be accessed as \Verb+this+.}

Division is replaced by multiplication which is easier to work with.
\[
n(n+1) = 2\sum_{i=r(n+1)} i\,.
\]
\begin{Verbatim}[firstnumber=last]
  apply Nat.div_eq_of_eq_mul_right this
    -- div_eq_of_eq_mul_right:
    --   (0 < n) ∧ (m = n * k) → m / n = k
\end{Verbatim}

The proof continues by induction, where !k! is the variable of induction and !ih! is the inductive hypothesis.
\begin{Verbatim}[firstnumber=last]
  induction' n with k ih
\end{Verbatim}

\boxed{Tactic: \Verb+induction'+}{induction}{To prove a goal with \Verb+n+ by induction, specify an induction variable \Verb+k+ and a name \Verb+ih+ for the inductive hypothesis.}

The base case $k=0$ is proved using Lean's simplifier.
\[
0(0+1) = 2*\sum_{i=0(0+1)} i = 2\cdot 0(0+1)
\]
\begin{Verbatim}[firstnumber=last]
  · simp
\end{Verbatim}

\boxed{Tactic: \Verb+simp+}{simp}{The \Verb+simp+ tactic uses lemmas and hypotheses in \Verb+Mathlib+ to simplify a goal or hypothesis.}
For the inductive step we assume that the theorem holds for $k$ and prove that it holds for $k+1$. The proof starts by separating out the last term of the sum. The notation is a bit clumsy because we aren't replacing $(k+1)+1$ by $k+2$.
\[
(k+1)((k+1)+1)=2\sum_{i=r((k+1)+1)} i=2\left(\sum_{i=r(k+1)} i + (k+1)\right)\,.
\]
\begin{Verbatim}[firstnumber=last]
  · rw [Finset.sum_range_succ]
      -- Finset.sum_range_succ:
      --   ∑ x in range (n + 1), f x = ∑ x in range n, f x + f n
\end{Verbatim}

Next, the distributive law is used.
\[
(k+1)((k+1)+1)=2\sum_{i=r(n)} i + 2(k+1)\,.
\]
\begin{Verbatim}[firstnumber=last]
    rw [mul_add 2]
      -- mul_add: a * (b + c) = a * b + a * c
\end{Verbatim}

Substitute the inductive hypothesis.
\[
(k+1)((k+1)+1)=k * (k+1) + 2(k+1)\,.
\]
\begin{Verbatim}[firstnumber=last]
    rw [← ih]
\end{Verbatim}
Lean uses the successor function which is now changed to $+1$ and the proof concludes with a simple calculation that can be performed by the tactic !ring!.
\begin{Verbatim}[firstnumber=last]
    rw [succ_eq_add_one]
      -- succ_eq_add_one : succ n = n + 1
    ring
  done
\end{Verbatim}

\subsection{Inductive definition of addition}

Here is an inductive definition of natural numbers.\footnote{The program is enclosed in the namespace \Verb+gentle+ to avoid clashing with predefined \Verb+Nat+. After defining \Verb+Nat+ we open its namespace so that we can use \Verb+zero+ and \Verb+succ+ without the prefix \Verb+Nat+.}
\begin{Verbatim}
inductive Nat
| zero : Nat
| succ : Nat → Nat
\end{Verbatim}

The next step is to defined addition inductively.
\begin{Verbatim}[firstnumber=last]
def add : Nat → Nat → Nat
  | x, zero => x
  | x, succ y => succ (add x y)
\end{Verbatim}
Although we normally think of addition as a function of two variables $\textit{add}(a,b)$, there are advantages to defining it as a function that takes one variable $\textit{add}(a)$ resulting in another function $\textit{add}_{a}(b)$ that gives the sum of $a$ and $b$.\footnote{This is called \emph{currying} but an explanation is outside the scope of this tutorial.}

\paragraph{A proof that $0+n=n$.}
The proof is by induction and the base case $0+0=0$ follows from the base case of the definition of !add!.
\begin{Verbatim}[firstnumber=last]
theorem zero_add (n : Nat) :
    add zero n = n := by
  induction' n with k ih
  · rfl
\end{Verbatim}

\boxed{Tactic: \Verb+rfl+}{rfl}{\Verb+rfl+ solves a goal that is an equality if they are \emph{definitionally equivalent}, that is, they define the same entity.}

Consider the goal:\\
\indnt{}!((m + k) + 1) + 1 = (m + (k + 1)) + 1!\\
\emph{By definition} of !add!, !(a + (b + 1)! is !(a + b) + 1! so the right-hand side of the equation !(m + (k + 1)) + 1! means !((m + k) + 1) + 1 ! which is the left-hand side. Therefore, the tactic !rfl! solves it.

The inductive step is to prove $0+(k+1)=(0+k)+1$. Using definition of !add! to rewrite the left-hand side, we get $(0+k)+1=k+1$ which can be solved using the inductive hypothesis that $0+k=k$.
\begin{Verbatim}[firstnumber=last]
  · rw [add]
    rw [ih]
  done
\end{Verbatim}

\paragraph{A proof that $(m+1)+n = (m+n)+1$.}
The base case $(m+1)+0=m+1$ is immediate from the base case of the definition of !add!.
\begin{Verbatim}[firstnumber=last]
theorem succ_add (m n : Nat) :
    add (succ m) n = succ (add m n) := by
  induction' n with k ih
  · rfl
\end{Verbatim}

The inductive step is to prove $(m+1) + (k+1) = (m + (k+1)) + 1$. From the definition of !add! it follows that $((m+1) + k) + 1 = (m + (k+1)) + 1$.
\begin{Verbatim}[firstnumber=last]
  · rw [add]
\end{Verbatim}
Substituting the inductive hypothesis $(m+1) + k = (m+k) + 1$ results in $((m+k) + 1) + 1 = (m + (k+1)) + 1$ which, as noted above can be solved by !rfl!.
\begin{Verbatim}[firstnumber=last]
    rw [ih]
    rfl
  done
\end{Verbatim}

\subsection{Multiple steps in a proof}

In this section we explain two ways to shorten proofs in Lean by performing more than one step in the same action. We start with the proof of $(m|n \vee m|k) \rightarrow m | mk$. Since the hypothesis is a disjunction, the goal must be proved separately for each disjunct using !rcases! to create separate hypotheses. Furthermore, since $m|n$ is defined as $\exists a (n=ma)$, new variables must be introduced (page~\pageref{p.rcases}).
\begin{Verbatim}
theorem div_prod1 {m n k : ℕ} (h : m ∣ n ∨ m ∣ k) :
    m ∣ n * k := by
  rcases h with ⟨a, h1⟩ | ⟨b, h2⟩
\end{Verbatim}
The new hypotheses are !h1 : n = m * a! and !h2 : k = m * b! which can be used to rewrite the goals. The rest of the proof is straightforward.
\begin{Verbatim}[firstnumber=last]
  · rw [h1]
    rw [mul_assoc]
    apply dvd_mul_right
      -- dvd_mul_right: a | a * b
  · rw [h2]
    rw [mul_comm]
    rw [mul_assoc]
    apply dvd_mul_right
  done
\end{Verbatim}

The proofs can be shortened by combining multiple rewrites into one !rw!.\\
\indnt{}!· rw [h1, mul_assoc]!\\
\indnt{}!· rw [h2, mul_comm, mul_assoc]!

The hypotheses !h1! and !h2! replace !m ∣ n! and !m ∣ k! by the definitionally equivalent !n = m * a! and !k = m * b!. !rfl! can be used to perform the substitutions without creating hypotheses.
\begin{Verbatim}
theorem div_prod2 {m n k : ℕ} (h : m ∣ n ∨ m ∣ k) :
    m ∣ n * k := by
  rcases h with ⟨a, rfl⟩ | ⟨b, rfl⟩
  · rw [mul_assoc]
    apply dvd_mul_right
  · rw [mul_comm, mul_assoc]
    apply dvd_mul_right
  done
\end{Verbatim}

\subsection{Calculating with equations and inequalities}

This subsection discusses proofs of theorem with inequalities. We prove the following theorem on Fibonacci numbers.
\[
\textrm{fib}(n) < (7/4)^n\,.
\]
The base cases are $\textrm{fib}(0) = 0 < (7/4)^0$ and $\textrm{fib}(1) = 1 < (7/4)^1$.\footnote{Fibonacci numbers are defined sometimes starting with $0$ and sometimes starting with $1$. Here, we start with $0$.} The inductive step is:
\begin{eqnarray*}
\textrm{fib}(n+1)&=&\textrm{fib}(n)+\textrm{fib}(n-1)<\left(\frac{7}{4}\right)^n + \left(\frac{7}{4}\right)^{n-1}\\
&=&\left(\frac{7}{4}\right)^{n-1}\left(\frac{7}{4}+1\right)<\left(\frac{7}{4}\right)^{n-1}\left(\frac{7}{4}\right)^2=\left(\frac{7}{4}\right)^{n+1}\,.
\end{eqnarray*}
The new aspects of the proof are: (a) two base cases and two inductive hypotheses, (b) an inductive hypothesis that is an inequality rather than an equality, and (c) calculations performed after using the inductive hypothesis.

We start with an inductive definition of the Fibonacci numbers. Normally, this is a function from natural numbers to natural numbers, but here the function is to rational numbers to enable comparison with $7/4$.
\begin{Verbatim}
def fib : ℕ → ℚ
  | 0 => 0
  | 1 => 1
  | n + 2 => fib (n + 1) + fib n
\end{Verbatim}
In the expression of the theorem $7/4$ is labeled as being of type !ℚ!, otherwise it would be interpreted as division of natural numbers with remainder.
\begin{Verbatim}[firstnumber=last]
theorem seven_fourths (n : ℕ) : fib n < (7 / 4 : ℚ) ^ n := by
\end{Verbatim}
The tactic !twoStepInduction! creates three subgoals, two for the base cases and one for the inductive step. It also creates two inductive hypotheses: !ih1! for !fib(k)! and !ih2! for !fib(k+1)!.
\begin{Verbatim}[firstnumber=last]
  induction' n using Nat.twoStepInduction with k ih1 ih2
\end{Verbatim}

\boxed{Tactic \Verb+twoStepInduction+}{two-step}{Proof by induction when there are two base cases and two inductive hypotheses.}

First the two base cases are proved.
\begin{Verbatim}[firstnumber=last]
  ·  rw [fib] ; norm_num
  ·  rw [fib] ; norm_num
\end{Verbatim}
\boxed{Tip Multiple tactics}{multiple}{A sequence of tactics can be written on one line with semicolons between them.}

Next we prove two hypotheses that will be used in the proof.
\begin{Verbatim}[firstnumber=last]
  · have h1 : 0 < (7/4:ℚ)^k := by apply pow_pos ; norm_num
      -- pow_pos: 0 < a → 0 < a ^ n
    have h2 : 1+(7/4:ℚ) < (7/4)^2 := by norm_num
\end{Verbatim}

Look again at the mathematical proof above, which involves substitution of the inductive hypotheses that are inequalities and calculation with rational numbers:
\[
e_1 = e_2 < e_3 = e_4 < e_5 = e_6\,.
\]
Of course each step in the calculation must be justified.

\newpage

This calculation can be performing using !calc!, where uniform indentation must be observed.\\
\indnt{}!calc e1 = e2 := by ...!\\
\indnt{}\indnt{}!_ < e3  := by ...!\\
\indnt{}\indnt{}!_ = e4  := by ...!\\
\indnt{}\indnt{}!_ < e5  := by ...!\\
\indnt{}\indnt{}!_ = e6  := by ...!

The definition of !fib! is used to expand !fib(k+2)! and the inductive hypotheses are used. Since the hypotheses are inequalities, we cannot use the !rw! tactic; instead, the !rel! tactic is used which substitutes inequalities.
\begin{Verbatim}[firstnumber=last]
    calc fib (k + 2) = fib (k + 1) + fib k := by rw [fib]
       _ < (7/4)^(k+1) + (7/4)^k := by rel [ih1, ih2]
       _ = (7/4)^k * (1+(7/4)) := by ring
       _ < (7/4)^k * (7/4)^2 := by
           apply (mul_lt_mul_left  h1).mpr h2
             -- mul_lt_mul_left: 0 < a → a * b < a * c ↔ b < c
     _ = (7/4)^(k+2) := by ring
  done
\end{Verbatim}

\boxed{Tactic \Verb+calc+\footnote{\Verb+calc+ is technically not a tactic, but I include it in the list of tactics for convenience.}}{calc}{A proof that is a sequence of expressions related by equality or inequality together with their proofs.}

\boxed{Tactic \Verb+rel+}{rel}{Given a hypothesis \Verb+h+ which is a relation $\mathit{e_1 \; rel\_op \; e_2}$, \Verb+rel [h]+ will attempt to prove the current goal by substituting \Verb+h+ into the goal.}

A shorter proof uses the tactic !gcongr! which is similar to !rel! except that the hypotheses need not be specified because the tactic will find them if possible.
\begin{Verbatim}
theorem seven_fourths_gcongr (n : ℕ) : fib n < (7 / 4 : ℚ) ^ n := by
  induction' n using Nat.twoStepInduction with k ih1 ih2
  ·  rw [fib] ; norm_num
  ·  rw [fib] ; norm_num
  · calc fib (k + 2) = fib (k + 1) + fib k := by rw [fib]
       _ < (7/4)^(k+1) + (7/4)^k := by gcongr
       _ = (7/4)^k * (1+(7/4)) := by ring
       _ < (7/4)^k * (7/4)^2 := by gcongr ; norm_num
       _ = (7/4)^(k+2) := by ring
  done
\end{Verbatim}

\newpage

Given\\
\indnt{}!fib (k + 2) = fib (k + 1) + fib k!\\
the tactic !gcongr! will find the (inductive) hypotheses by itself so we have a proof of that\\
\indnt{}!fib (k + 2) < (7/4)^(k+1) + (7/4)^k!

\boxed{Tactic \Verb+gcongr+}{gcongr}{\Verb+gcongr+ will attempt to prove the current goal by finding and substituting hypotheses of the form $\mathit{e_1 \; rel\_op \; e_2}$ into the goal.}
