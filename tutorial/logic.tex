% !TeX root = gentle.tex
% !TeX Program=XeLaTeX

\section{Propositional logic}

\subsection{Conjunction and equivalence}

When a theorem has equivalence (!↔!) as the main operator, it must be split into two subgoals, one for the modus ponens (!mp!) direction and one for the modus ponens reversed (!mpr!) direction.

\begin{Verbatim}
theorem lt_iff_le_eq {a b : Int} : 
    a < b ↔ a ≤ b ∧ a ≠ b := by
  rw [lt_iff_le_not_le]
    -- lt_iff_le_not_le : a < b ↔ (a ≤ b ∧ ¬b ≤ a)
  constructor
    -- Create two subgoals (mp and mpr) from the current iff goal
    -- First subgoal is (a ≤ b ∧ ¬b ≤ a) → (a ≤ b ∧ a ≠ b)
 \end{Verbatim}

The current goal is an implication whose premise and conclusion are both conjunctions. We can use !intro! to introduce the premise as a hypothesis and then !rcases! (page~\pageref{p.rcases}) to split the conjunctive hypothesis into two subgoals to be proved.\\
\indnt{}!intro h!\\
\indnt{}!rcases h with ⟨h0, h1⟩!\\
It is possible to combine the two tactics into the tactic !rintro!.

We start with the implication of the modus ponens direction, where !rintro! introduces the hypothesis and then !constructor! splits the conjuctive goal into two subgoals.
\begin{Verbatim}[firstnumber=last]
  · rintro ⟨h0, h1⟩
    -- Introduces the premise as a hypothesis and split it
    --   into two sub-hypotheses a ≤ b and ¬b ≤ a

    constructor
      -- Creates two subgoals from the current conjunctive goal
    · exact h0
        -- Proves the second subgoal
    · intro h2
        -- a ≠ b is a = b → False
      apply h1
        -- Replace False with the negation of the hypothesis
      rw [h2]
\end{Verbatim}

The proof of the implication of the modus ponens reversed direction is similar.
\begin{Verbatim}[firstnumber=last]
  · rintro ⟨h0, h1⟩
    constructor
    · exact h0
    · intro h2
      apply h1
      apply le_antisymm h0 h2
        --  le_antisymm: (a ≤ b) → (b ≤ a) → a = b
  done
\end{Verbatim}

\boxed{Tactic: \Verb+constructor+}{constructor}{Splits a goal into two subgoals: equivalence (\Verb+↔+) into two implications (Modus ponens and Modus ponens reversed) and conjunction (\Verb+∧+) into two conjuncts.}


\boxed{Tactic: \Verb+rintro+}{rintro}{Performs \Verb+intro+ and then \Verb+rcases+ to split the resulting hypothesis.}

\subsection{Disjunction}

In the previous theorem, we first split the equivalence into two implications. Here, tactic !rcases! is used with a theorem to split on the sign of $y$. \begin{Verbatim}
theorem lt_abs {x y : Int} :
    x < |y| ↔ x < y ∨ x < -y := by
  rcases le_or_gt 0 y with h | h
    -- Absolute value depends on sign of y
    -- le_or_gt 0 y: a ≤ b ∨ a > b
\end{Verbatim}

The equivalence goal is unchanged, but we must prove it under both hypotheses: $0\leq y$ and $0>y$. First we prove the goal for $0\leq y$, in which case we can rewrite $|y|$ by $y$.
\begin{Verbatim}[firstnumber=last]
  · rw [abs_of_nonneg h]
    -- abs_of_nonneg: 0 ≤ a → |a| = a
    constructor
      -- Split iff into mp and mpr
\end{Verbatim}
For the modus ponens direction, the premise is introduced as a hypothesis and the  conclusion becomes the goal $x < y \vee x < -y$. When a \emph{goal} is a disjunction, it is sufficient to prove one disjunct. Here we are smart enough to tell Lean that we want to prove the left disjunct, because the right one won't make any progress toward the proof.
\begin{Verbatim}[firstnumber=last]
    · intro h'
      left
      exact h'
\end{Verbatim}
For the modus ponens reversed direction, the \emph{hypothesis} is a disjunction and we have to prove the goal for each disjunct. For the second disjunct, we have $0 \le y$ and $x < -y$, and Lean can prove that this implies the goal $x < y$ using the tactic !linarith!.

\begin{Verbatim}[firstnumber=last]
    . intro h'
      rcases h' with h' | h'
      · exact h'
      . linarith
\end{Verbatim}

\boxed{Tactic: \Verb+left+, \Verb+right+}{left-right}{If the goal is a disjunction, tell Lean which disjunction you want to prove.}

\boxed{Tactic: \Verb+linarith+}{linarith}{The tactic solves linear equalities and inequalities. Unlike \Verb+ring+ it can use hypotheses and unlike \Verb+norm\_num+ it can solve equations with variables.}

The proof for $y< 0$ is similar.
\begin{Verbatim}[firstnumber=last]
  · rw [abs_of_neg h]
    constructor
    · intro h'
      right
      exact h'
    . intro h'
      rcases h' with h' | h'
      · linarith
      . exact h'
  done
\end{Verbatim}

\subsection{Implication}

The following theorem is proved by contradiction using the tactic !by_contra!, after which the proof is straightforward.
\begin{Verbatim}
theorem T1a {A : Prop} : (¬A → A) → A := by
  intro h1
  by_contra h2
    -- Prove A by contradction: assume A and prove False
  apply h2
  apply h1
  exact h2
  done
\end{Verbatim}

\boxed{Tactic: \Verb+by\_contra+}{by-contra}{This tactic removes a goal \Verb+P+, adds the hypothesis \Verb+¬P+ and creates a new goal \Verb+False+.}

\newpage

Here is another proof of the same theorem using contraposition instead of contradiction.
\begin{Verbatim}[firstnumber=last]
theorem T1b {A : Prop} : (¬A → A) → A := by
  intro h1
  contrapose! h1
    -- Replace h1 by its contrapositive and push negation inward
  constructor
  · exact h1
  · exact h1
  done
\end{Verbatim}

The result of using the tactic !contrapose! is not what you might expect from the way it is usually used in logic. Given the \emph{hypothesis} $\neg A \rightarrow A$, it does not change the hypothesis into $\neg A \rightarrow \neg\neg A$. Instead, the hypothesis becomes the goal $\neg(\neg A \rightarrow A)$ and the goal $A$ becomes the hypothesis $\neg A$.

This makes sense by the deduction theorem.
\[
\{H_1, H_2, \ldots, H_n\} \vdash G 
\]
means 
\[
\vdash H_1\wedge H_2 \wedge \ldots \wedge H_n \rightarrow G
\]
whose contrapositive is
\[
\vdash \neg G \rightarrow \neg (H_1\wedge H_2 \wedge \ldots \wedge H_n)\,,
\]
which by the deduction theorem is
\[
\neg G \vdash \neg (H_1\wedge H_2 \wedge \ldots \wedge H_n)\,.
\]

Although $\neg(\neg A \rightarrow A)$ doesn't simplify the proof, if we push negation inward (do the calculation yourself\verb+!+) the result is $\neg A \wedge \neg A$ which is trivial to prove. The exclamation point in the tactic means that following the tactic !contrapose!, the tactic \Verb+push_neg+ is called to push negation inward.

\boxed{Tactic: \Verb+contrapose+}{contrapose}{Transforms a goal into its contrapositive. Applied to a hypothesis, it makes the negation of the goal into a hypothesis and the negation of the hypothesis into the goal. 
An exclamation point following \Verb+contrapose+ calls tactic \Verb+push\_neg+ on the resulting contrapositive.}

The interesting step in the following proof is \\
\indnt{}!rcases h1 with ⟨h2, _⟩!\\
The hypothesis !h1! is !A ∧ ¬B! while the goal is !A!. We use !rcases! to split the hypothesis into two, !A! and !¬B!. Since only one sub-hypothesis is sufficient to prove the goal, we don't even bother to give the sub-hypothesis !¬B! a name.
\begin{Verbatim}[firstnumber=last]
theorem T2 {A B : Prop} : ((A → B) → A) → A := by
  intro h1
  contrapose! h1

  constructor
  · contrapose! h1
      -- Make ¬(A → B) the hypothesis and A the goal
      -- Push negation inward
    rcases h1 with ⟨h2, _⟩
      -- Split the hypothesis
    · exact h2
      -- Only need to use left subformula
  · exact h1
  done
\end{Verbatim}

\boxed{Tip: Don't care}{dont-care}{When a name or value is syntactically required but you don't care what its value is, you can use the underscore symbol \Verb+\_+ instead.}

The theorem $(A\rightarrow B) \vee (B \rightarrow C)$ has a disjunction operator as its main operator. We prefer to carry out the proof using only the implication operator, so we first try to prove the hypthosis $\neg(A\rightarrow B) \rightarrow (B \rightarrow C)$. When the hypothesis has been proved, we use the \Verb+contrapose!+ to negate the hypothesis and the goal, and then exchange them. Moving the negations inward results in a hypothesis that is exactly the goal.
\begin{Verbatim}[firstnumber=last]
theorem T3 {A B C : Prop} : (A → B) ∨ (B → C) := by
  have h1 : ¬(A → B) → (B → C) := by
    intro h2
    intro h3
    contrapose! h2
    intro
      -- No need to name the new hypothesis
    exact h3
  contrapose! h1
  exact h1
  done
\end{Verbatim}

\newpage

\subsection{Tautologies}

Tautologies in propositional logic can be proved very easily using semantic methods such as truth tables and semantic tableaux. Lean can prove tautologies so the above theorems can be proved immediately using the tactic !tauto!.

\begin{Verbatim}[numbers=none]
theorem T1a_tau {A : Prop}     : (¬A → A) → A      := by tauto
theorem T2_tau  {A B : Prop}   : ((A → B) → A) → A := by tauto
theorem T3_tau  {A B C : Prop} : (A → B) ∨ (B → C) := by tauto
\end{Verbatim}
\boxed{Tactic: \Verb+tauto+}{tauto}{If a tautology can be formed from the hypotheses and the goal, the proof can be immediately completed using this tactic.}
The following theorem shows that !tauto! works when the tautology is formed from both a hypothesis and a goal.\\
\indnt{}!theorem T0a_tau {A : Prop} (h : ¬A → A) : A := by tauto!\\

This theorem shows that !tauto! works on substitution instances of a tautology.\\
\indnt{}!theorem T0b_tau {a : Nat} : ¬a = 0 ∨ a = 0 := by tauto!
