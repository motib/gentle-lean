% !TeX root = gentle.tex
% !TeX Program=XeLaTeX

\section{Cancellation properties}

Here we prove theorems of arithmetic related to cancellation properties:
\[
-a+(a+b) =b\,,\quad a+c=a+c \rightarrow b= c\,,\quad a*0 =0.
\]

The library !import Mathlib.Data.Int.Basic! is imported because negative numbers are integers, not natural numbers.

\boxed{Tip: associativity}{associativity}{The associativity of the operators \Verb=+= and \Verb+*+ is defined to be \emph{left}:\\\smallskip
\verb!a + b + c! is \verb!(a + b) + c! and \verb!a * b * c! is \verb!(a * b) * c!.\\\smallskip
Use \Verb+add\_assoc+ and \Verb+mul\_assoc+ to change the associativity of an expression.}

In order to prove $- a + (a + b)$ we first need to proof that it equals $(-a + a) + b)$ and then set $-a + a = 0$.

This proof uses the tactic !rw! that \emph{rewrites} an expression with another expression that has already been proved to be equal to it. After two rewrites, applying the theorem !zero_add b! results in exactly the expression needed to proof the goal.

\begin{Verbatim}
theorem neg_add_cancel_left (a b : Int) :
    -a + (a + b) = b := by
  rw [← add_assoc]
    -- add_assoc: a + b + c = a + (b + c)
    -- Addition is left associative: a + b + c = (a + b) + c
    -- New goal is -a + a + b = b
  rw [add_left_neg]
    -- add_left_neg: -a + a = 0
    -- New goal is 0 + b = b
  exact zero_add b
    -- zero_add: 0 + a = a, where a = b
  done
\end{Verbatim}

\boxed{Tactic: \Verb+rw+}{rw}{\Verb+rw [eqn]+, where \Verb+eqn+ is an equation or an equivalence, rewrites the goal by replacing occurrences of the the left-hand side of \Verb+eqn+ with the right-hand side.\\
\Verb+rw [← eqn]+ rewrites the goal by replacing occurrences of the right-hand side of \Verb+eqn+ with the left-hand side.\\
\Verb+rw [eqn] h+ and \Verb+rw [← eqn] h+ rewrite expressions in the hypothesis \Verb+h+.}

The arrow !←! which is defined as ``rewrites the goal by replacing occurrences of the right-hand side of \Verb+eqn+ with the left-hand side'' can seem non-intuitive: when !← add_assoc! is used to rewrite the goal !-a + (a + b) = b! above, we are rewriting the \emph{left-hand side} of the goal. However, the directions are referring to the sides of \Verb+eqn+ (here, !a + b + c = a + (b + c)!) where occurrences of \emph{its} right-hand side are !a + (b + c)! are replaced by \emph{its} left-hand side !a + b + c!.

\boxed{Tactic: \Verb+exact+}{exact}{The tactic \Verb+exact h+ is used when \Verb+h+ is exactly the statement of the current goal, so the goal is now proved. \Verb+exact+ is similar to \Verb+apply+ but limited because it can only be used if the hypothesis exactly matches the goal.}

The parameter !h : a + b = a + c! in the theorem !add_left_cancel! is a hypothesis that is used in the proof. It must be provided when the theorem is applied (see below).
\begin{Verbatim}[firstnumber=last]
theorem add_left_cancel {a b c : Int}
    (h : a + b = a + c) : b = c := by
  rw [← neg_add_cancel_left a b]
    -- neg_add_cancel (above): -a + (a + b) = b
    -- New goal is -a + (a + b) = c
  rw [h]
    -- New goal is -a + (a + c) = c
  exact neg_add_cancel_left a c
    -- neg_add_cancel (above): -a + (a + b) = b, where b = c
  done
\end{Verbatim}

The theorem !add_left_cancel! has four parameters: !a b c! of type !Int! and a hypothesis !h!. To use the theorem we can write\\
\indnt{}!apply add_left_cancel i j k h!\\
for some values !i j k! of type !Int! and a hypothesis !h!. When the theorem is used in line~36 below, !h! has been replaced by its definition in line~25:\\
\indnt{}!exact add_left_cancel (a * 0 + a * 0 = a * 0 + 0)!\\
but the integer parameters are not given. When the hypothesis is matched against !a + b = a + c! in the statement of the theorem, we have\\
\indnt{}!a = a * 0! $\quad$ !b = a * 0!$\quad$   !c = 0!.\\
Clearly, all three expressions are of type !Int! so Lean can infer their values and types when they are used:\\
\indnt{}!exact add_left_cancel (a * 0) (a * 0) 0 h!

\boxed{Tip: implicit}{implicit}{Parameters declared with braces, such as \Verb+\{a b c : Int\}+ state that \Verb+a b c+ are \emph{implicit} bound variables in the theorem whose actual names and types can be inferred when the theorem is used.}

The proof of the following theorem is similar to the previous two. First the hypothesis !h! is proved using !rw! three times and it is then used as a hypothesis in the application of !add_left_cancel!.
\begin{Verbatim}[firstnumber=last]
theorem mul_zero {a : Int} :
    a * 0 = 0 := by
  have h : a * 0 + a * 0 = a * 0 + 0 := by
    rw [← mul_add]
      -- Distribute multiplcation over addition (reversed)
      -- mul_add: a * (b + c) = a * b + a * c,
      --   where a = a, b = 0, c = 0
      -- New goal is a * (0 + 0) = a * 0 + 0
    rw [add_zero]
      -- add_zero: a + 0 = 0
      -- New goal is a * 0 = a * 0 + 0
    rw [add_zero]
      -- h is proved
  exact add_left_cancel h
    -- add_left_cancel (above): if a + b = a + c then b = c,
    --   where a = a * 0, b = 0, c = 0
  done
\end{Verbatim}
